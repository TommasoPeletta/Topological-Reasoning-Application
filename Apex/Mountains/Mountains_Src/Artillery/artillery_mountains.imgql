import "stdlib.imgql"

// filter function definition
let flt(x,a) = distleq(x,distgeq(x,!a))

// load original image
load image = "mountain_artillery.png"

// read image components
let r = red(image)
let g = green(image)
let b = blue(image)


// define zone of interest
let zone = distleq(250,((r >. 250) & (g <. 11) & (b <. 11)))

//colour treshold
let treshold = (r >. 55) & (g >. 55) & (b >. 25) & (r <. 150) & (g <. 100) & (b <. 45)


// smoothing filters
let filtered1 = !flt(3,!treshold)
let filtered2 = !touch(!filtered1, border)
let filtered4 = flt(6,flt(3, filtered2))



// Zone of interest only
let seg = (zone & filtered4) & !touch(zone & filtered4 , !zone)

// application of original image on result
let coloured = rgb(mask(r,seg),mask(g,seg),mask(b,seg))


// save outputs
save "output/Zone.png" zone
save "./output/treshold.png" treshold
save "./output/filtered1.png" filtered1
save "./output/filtered2.png" filtered2
save "./output/filtered3.png" filtered4
save "./output/original.png" image
save "./output/coloured.png" coloured
save "../../output/artilleryMountSeg.png" seg
